\chapter{\RU{Перестановка basic block-ов}\EN{Basic blocks reordering}}

% TODO __builtin_expect in GCC?

\section{Profile-guided optimization}
\label{PGO}

\index{\oracle}
\index{Intel C++}

\RU{Этот метод оптимизации кода может перемещать некоторые \gls{basic block}-и в другую секцию
исполняемого бинарного файла}
\EN{This optimization method can move some \gls{basic block}s to another section of the executable binary
file}.

\RU{Очевидно, в функции есть места которые исполняются чаще всего (например, тела циклов)
и реже всего (например, код обработки ошибок, обработчики исключений)}
\EN{Obviously, there are parts of a function which are executed more frequently (e.g., loop bodies)
and less often (e.g., error reporting code, exception handlers)}.

\RU{Компилятор добавляет дополнительный (instrumentation) код в исполняемый файл,
затем разработчик запускает его с тестами для сбора статистики.}
\EN{The compiler adds instrumentation code into the executable, then the developer runs it with
a lot of tests to collect statistics.}
\RU{Затем компилятор, при помощи собранной статистики, приготавливает итоговый исполняемый
файл где весь редко исполняемый код перемещен в другую секцию}
\EN{Then the compiler, with the help of the statistics gathered,
prepares final the executable file with all infrequently executed code moved into another section}.

\RU{В результате, весь часто исполняемый код функции становится компактным, что очень важно для скорости
исполнения и кэш-памяти}
\EN{As a result, all frequently executed function code is compacted, and that is very important
for execution speed and cache usage}.

\RU{Пример из \oracle, который скомпилирован при помощи Intel C++}
\EN{An example from \oracle code, which was compiled with Intel C++}:

\begin{lstlisting}[caption=orageneric11.dll (win32)]
                public _skgfsync
_skgfsync       proc near

; address 0x6030D86A

                db      66h
                nop
                push    ebp
                mov     ebp, esp
                mov     edx, [ebp+0Ch]
                test    edx, edx
                jz      short loc_6030D884
                mov     eax, [edx+30h]
                test    eax, 400h
                jnz     __VInfreq__skgfsync  ; write to log
continue:
                mov     eax, [ebp+8]
                mov     edx, [ebp+10h]
                mov     dword ptr [eax], 0
                lea     eax, [edx+0Fh]
                and     eax, 0FFFFFFFCh
                mov     ecx, [eax]
                cmp     ecx, 45726963h
                jnz     error                ; exit with error
                mov     esp, ebp
                pop     ebp
                retn
_skgfsync       endp

...

; address 0x60B953F0

__VInfreq__skgfsync:
                mov     eax, [edx]
                test    eax, eax
                jz      continue
                mov     ecx, [ebp+10h]
                push    ecx
                mov     ecx, [ebp+8]
                push    edx
                push    ecx
                push    offset ... ; "skgfsync(se=0x%x, ctx=0x%x, iov=0x%x)\n"
                push    dword ptr [edx+4]
                call    dword ptr [eax] ; write to log
                add     esp, 14h
                jmp     continue

error:
                mov     edx, [ebp+8]
                mov     dword ptr [edx], 69AAh ; 27050 "function called with invalid FIB/IOV structure"
                mov     eax, [eax]
                mov     [edx+4], eax
                mov     dword ptr [edx+8], 0FA4h ; 4004
                mov     esp, ebp
                pop     ebp
                retn
; END OF FUNCTION CHUNK FOR _skgfsync
\end{lstlisting}

\RU{Расстояние между двумя адресами приведенных фрагментов кода почти 9 МБ.}
\EN{The distance of addresses between these two code fragments is almost 9 MB.}

\RU{Весь редко исполняемый код помещен в конце секции кода DLL-файла, среди редко
исполняемых частей прочих функций}
\EN{All infrequently executed code was placed at the end of the code section of the DLL file,
among all function parts}.
\RU{Эта часть функции была отмечена компилятором Intel C++ префиксом \TT{VInfreg}}
\EN{This part of the function was marked by the Intel C++ compiler with the \TT{VInfreq} prefix}.
\RU{Мы видим часть функции которая записывает в лог-файл (вероятно, в случае ошибки или предупреждения,
или чего-то в этом роде) которая, наверное, не исполнялась слишком часто, когда разработчики Oracle
собирали статистику (если вообще исполнялась).}
\EN{Here we see that a part of the function that writes to a log file (presumably in case of error or warning
or something like that) which was probably not executed very often when Oracle's developers gathered 
statistics (if it was executed at all).}
\RU{Basic block записывающий в лог-файл, в конце концов возвращает управление в \q{горячую} часть
функции}
\EN{The writing to log basic block eventually returns the control flow to the \q{hot} part of the function.}

\RU{Другая \q{редкая} часть --- это \gls{basic block} возвращающий код ошибки 27050}
\EN{Another \q{infrequent} part is the \gls{basic block} returning error code 27050}.

\RU{В ELF-файлах для Linux весь редко исполняемый код перемещается компилятором Intel C++
в другую секцию (\TT{text.unlikely}) оставляя весь \q{горячий} код в секции \TT{text.hot}}
\EN{In Linux ELF files, all infrequently executed code is moved by Intel C++ into the separate 
\TT{text.unlikely} section, leaving all \q{hot} code in the \TT{text.hot} section}.

\RU{С точки зрения reverse engineer-а, эта информация может помочь разделить функцию на её основу
и части, отвечающие за обработку ошибок}
\EN{From a reverse engineer's perspective, this information may help to split the function
into its core and error handling parts}.
