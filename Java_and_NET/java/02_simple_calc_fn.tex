% TODO proof-reading
\section{\EN{Simple calculating functions}\RU{Простая вычисляющая функция}}

\EN{Let's continue with simple calculating functions}\RU{Продолжим с простой вычисляющей функцией}.

\begin{lstlisting}
public class calc
{
	public static int half(int a)
	{
		return a/2;
	}
}
\end{lstlisting}

\EN{This is the case when \TT{iconst\_2} instruction is used:}
\RU{Это тот случай, когда используется инструкция \TT{iconst\_2}:}

\begin{lstlisting}
  public static int half(int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=1, args_size=1
         0: iload_0       
         1: iconst_2      
         2: idiv          
         3: ireturn       
\end{lstlisting}
         
\TT{iload\_0} \EN{takes zeroth function argument and pushes it to stack.}
\RU{Берет нулевой аргумент функции и заталкивает его в стек.}
\TT{iconst\_2} \EN{pushes 2 into stack}\RU{заталкивает в стек 2}.
\EN{After these two instructions execution, this is how stack looks like:}
\RU{Вот как выглядит стек после исполнения этих двух инструкций:}

% FIXME: TikZ
\begin{lstlisting}
      +---+
TOS ->| 2 |
      +---+
      | a |
      +---+
\end{lstlisting}

\EN{\TT{idiv} just takes two values at the \ac{TOS}, divides one by another and leave
result at \ac{TOS}:}
\RU{\TT{idiv} просто берет два значения на вершине стека (\ac{TOS}), 
делит одно на другое и оставляет результат на вершине (\ac{TOS}):}

% FIXME: TikZ
\begin{lstlisting}
      +--------+
TOS ->| result |
      +--------+
\end{lstlisting}

\TT{ireturn} \EN{takes it and returns}\RU{берет его и возвращает}.

\EN{Let's proceed to double precision floating point numbers:}
\RU{Продолжим с числами с плавающей запятой, двойной точности:}

\begin{lstlisting}
public class calc
{
	public static double half_double(double a)
	{
		return a/2.0;
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Constant pool]
...
   #2 = Double             2.0d
...
\end{lstlisting}

\begin{lstlisting}
  public static double half_double(double);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=2, args_size=1
         0: dload_0       
         1: ldc2_w        #2                  // double 2.0d
         4: ddiv          
         5: dreturn       
\end{lstlisting}

\EN{It's the very same, but \TT{ldc2\_w} instruction is used to load constant of 
2.0 from constant pool.}
\RU{Почти то же самое, но инструкция \TT{ldc2\_w} используется для загрузки константы 
2.0 из пула констант.}
\EN{Also, all other three instructions has \IT{d} prefix, 
meaning they work with \IT{double} data type values.}
\RU{Также, все три инструкции имеют префикс \IT{d}, что означает, что они работают с переменными
типа \IT{double}.}

\EN{Let's now use two arguments function:}
\RU{Теперь перейдем к функции с двумя аргументами:}

\begin{lstlisting}
public class calc
{
	public static int sum(int a, int b)
	{
		return a+b;
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static int sum(int, int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=2, args_size=2
         0: iload_0       
         1: iload_1       
         2: iadd          
         3: ireturn       
\end{lstlisting}

\EN{\TT{iload\_0} loads first function argument (a), \TT{iload\_2}\EMDASH{}second (b).}
\RU{\TT{iload\_0} загружает первый аргумент функции (a), \TT{iload\_2}\EMDASH{}второй (b).}
\EN{Here is a stack after both instruction executed:}
\RU{Вот так выглядит стек после исполнения обоих инструкций:}

\begin{lstlisting}
      +---+
TOS ->| b |
      +---+
      | a |
      +---+
\end{lstlisting}

\EN{\TT{iadd} adds two values and leave result at \ac{TOS}:}
\RU{\TT{iadd} складывает два значения и оставляет результат на \ac{TOS}:}

\begin{lstlisting}
      +--------+
TOS ->| result |
      +--------+
\end{lstlisting}

\EN{Let's extend this example to \IT{long} data type:}
\RU{Расширим этот пример до типа данных \IT{long}:}

\begin{lstlisting}
	public static long lsum(long a, long b)
	{
		return a+b;
	}
\end{lstlisting}

\dots \EN{we got}\RU{получим}:

\begin{lstlisting}
  public static long lsum(long, long);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=4, args_size=2
         0: lload_0       
         1: lload_2       
         2: ladd          
         3: lreturn       
\end{lstlisting}

\EN{The second \TT{lload} instruction takes second argument from 2nd slot.}
\RU{Вторая инструкция \TT{lload} берет второй аргумент из второго слота.}
\EN{That's because 64-bit \IT{long} value occupies exactly two 32-bit slots.}
\RU{Это потому что 64-битное значение \IT{long} занимает ровно два 32-битных слота.}

\EN{Slightly more complex example:}
\RU{Немного более сложный пример:}

\begin{lstlisting}
public class calc
{
	public static int mult_add(int a, int b, int c)
	{
		return a*b+c;
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static int mult_add(int, int, int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=3, args_size=3
         0: iload_0       
         1: iload_1       
         2: imul          
         3: iload_2       
         4: iadd          
         5: ireturn       
\end{lstlisting}

\EN{First step is multiplication. Product is left at the \ac{TOS}:}
\RU{Первый шаг это умножение. Произведение остается на \ac{TOS}:}

\begin{lstlisting}
      +---------+
TOS ->| product |
      +---------+
\end{lstlisting}

\TT{iload\_2} \EN{loads third argument (c) into stack}\RU{загружает третий аргумент (c) в стек}:

\begin{lstlisting}
      +---------+
TOS ->|    c    |
      +---------+
      | product |
      +---------+
\end{lstlisting}

\EN{Now \TT{iadd} instruction can add two values.}
\RU{Теперь инструкция \TT{iadd} может сложить два значения.}
